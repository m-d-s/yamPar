//options { DEBUG_PARSER = true; } 
PARSER_BEGIN(Parser)
import ast.*;
public class Parser {
  public static void main(String[] args) {
     new Parser(System.in);
     try {
       Stmt s = Top();
       new IndentOutput(System.out).indent(s);
     } catch (ParseException e) {
       System.out.println("Invalid syntax at ("
                          + token.beginColumn + ","
                          + token.beginLine + "), "
                          + token.image);
     } catch (TokenMgrError e) {
       System.out.println(e.getMessage());
     }
  }
}
PARSER_END(Parser)

//- Parser Functions --------------------------------------------------

Stmt Top(): { Stmt s; } {
  s=Stmt() <EOF> { return s; }
}

Stmt Stmt(): { Stmt s, l = null; Expr e = null; StmtExpr m; } {
  "print" e=Expr() ";"                     { return new   Print(e);  }
 |"return" ( e=Expr() )? ";"               { return new  Return(e);  }
 | s=Block()                               { return s;               }
 | "while" <OPEN>e=Expr()<CLOSE>  s=Stmt() { return new While(e,s);  }
 | "if"    <OPEN>e=Expr()<CLOSE>  s=Stmt()
     (LOOKAHEAD(1) "else" l=Stmt())?       { return new  If(e,s,l);  } 
 | s=Decl()                                { return s;               }
 | m=StdAln()				   { return new ExprStmt(m); }
 | ";"					   { return new Empty();     }
}

Stmt Block(): { Stmt[] stmts; } {
  "{" stmts=Stmts(0) "}"
  { return new Block(stmts); }
}

Stmt[] Stmts(int soFar): { Stmt s; Stmt[] stmts; } {
  (s=Stmt() stmts=Stmts(soFar+1)
    { stmts[soFar]=s; return stmts; }
  | { return new Stmt[soFar]; })
}

Stmt Decl(): { Type y; VarIntro[] intros; } {
  y=Type() intros = Intros(0) ";"
  { return new Locals(y,intros); } 
}

VarIntro Var(): { Token t; Expr e; } {
  t=<IDENT> ( "=" e=Expr() { return new InitVarIntro(t.image,e); })?
    { return new VarIntro(t.image); } 
}

VarIntro[] Intros(int soFar): { VarIntro[] intros; VarIntro v; } {
  v=Var() ("," intros = Intros(soFar+1) { intros[soFar] = v; return intros;})?
  intros=Intros(soFar+1) { intros[soFar] = v; return intros;} 
  |  { return new VarIntro[soFar]; }  
}

Type Type(): { } {
  ( "int"     { return Type.INT; }
  | "boolean" { return Type.BOOLEAN; })
}

StmtExpr StdAln(): { Expr e; Token t; } {
  t=<IDENT> "=" e=Expr() ";"  { return new Assign(t.image, e); }
} 

Expr Expr(): { Expr e; Token t; } {
  LOOKAHEAD(2)
  t =<IDENT> "=" e=Expr()   { return new Assign(t.image, e); }
  |e=LogOr()                { return e;        }
}

Expr LogOr(): { Expr e, p; } {
  e=LogAnd() ( <LOR> p =LogOr() { e = new LOr(e,p); } )?
    { return e; }
}
Expr LogAnd(): { Expr e, p; } {
  e=Comp() ( <LAND> p=LogAnd() { e = new LAnd(e,p); }   )?
    { return e; }
}

Expr Comp(): { Expr e, p; } {
  e=AddSub() ( 
    ( <LT>   p=AddSub() { e = new Lt(e,p);  }      )
   |( <LTEQ> p=AddSub() { e = new Lte(e,p); }      )
   |( <GT>   p=AddSub() { e = new Gt(e,p);  }      )
   |( <GTEQ> p=AddSub() { e = new Gte(e,p); }      )
   |( <NEQ>  p=AddSub() { e = new Neq(e,p); }      )
   |( <EQEQ> p=AddSub() { e = new Eql(e,p); }      )
  )?
    { return e; }
}

Expr AddSub(): { Expr e, p; } {
  e=MulDiv() ( 
	       ( <ADD> p = MulDiv() { e = new Add(e,p); } ) 
	      |( <SUB> p = MulDiv() { e = new Sub(e,p); } )
	     )*
    { return e; }
}

Expr MulDiv(): { Expr e, p; } {
  e=Atom() ( ( <MUL> p = Atom() { e = new Mul(e,p); } )
            |( <DIV> p = Atom() { e = new Div(e,p); } )
	   )*
    { return e; }
}

Expr Atom(): { Expr e; Token t; } {
    t=<IDENT>               { return new Id(t.image);     }  
  | t=<INTLIT>              { return new IntLit(Integer.parseInt(t.image)); } 
  | <OPEN> e=Expr() <CLOSE> { return e;            } 
  
}


//- Lexical Rules -----------------------------------------------------

//Whitespace and comments
SKIP : {
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
| <"/*" (~["*"])* ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

//Integer literal
TOKEN : {
  <INTLIT:     (<DIGIT>)+>
| <#DIGIT:     ["0"-"9"] >
}

//Identifier
TOKEN : {
  <IDENT:      <BEGIN> ( <LETTER> | <DIGIT> )*>
| <#BEGIN:     ["A"-"Z","a"-"z"]            >
| <#LETTER:    <BEGIN> | ["_"]              >
}

//Parentheses
TOKEN : {
  <OPEN:       "(" >
| <CLOSE:      ")" >
}

//Comparitor Operaters
TOKEN : {
  <LT:         "<" >
| <LTEQ:       "<=">
| <GT:         ">" >
| <GTEQ:       ">=">
| <NEQ:        "!=">
| <EQEQ:       "==">
}

//Logical Operaters
TOKEN : {
  <LAND:       "&&">
| <LOR:        "||">
}

//Arithmetic Operaters
TOKEN : {
  <ADD:        "+" >
| <SUB:        "-" >
| <MUL:        "*" >
| <DIV:        "/" >
}
